plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'maven-publish'
    id "com.diffplug.spotless" version "6.13.0"
}

ext {
    oshi_hack = project.properties['oshi_hack'] ?: false
}

def baseVersion = "${version_major}.${version_minor}.${version_patch}"

group 'me.hypherionmc.sdlink'
version = baseVersion + (oshi_hack ? "h" : "")
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://maven.firstdarkdev.xyz/releases" }
    maven { url "https://maven.firstdarkdev.xyz/snapshots" }
    maven { url "https://m2.dv8tion.net/releases" }
    maven { url "https://nexus.velocitypowered.com/repository/maven-public/" }
}

configurations {
    shade
    shade.transitive = true
    implementation.extendsFrom(shade)
}

dependencies {
    // Compile Only, Not Shadowed

    // Logging
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4j_ver
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4j_ver
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j18-impl', version: log4j_ver

    // Utilities
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: commons_ver

    // Core Dependencies, Shadowed

    // Discord
    shade ("pw.chew:jda-chewtils:${jdautils_ver}") { exclude(group: 'org.apache.commons') }
    shade("net.dv8tion:JDA:$jdaVersion") { exclude module: 'opus-java' exclude(group: 'org.apache.commons') }
    shade("club.minnced:discord-webhooks:${webhooks_ver}")

    // Utilities
    shade("org.apache.commons:commons-collections4:${commons4_ver}")
    shade group: 'org.json', name: 'json', version: json_ver
    shade group: 'com.github.oshi', name: 'oshi-core', version: oshi_ver

    shade "me.hypherionmc.moon-config:core:${moonconf_ver}"
    shade "me.hypherionmc.moon-config:toml:${moonconf_ver}"
    shade("me.hypherionmc:JQLite:${jqlite_ver}") { exclude(group: 'org.apache.commons') }
}

jar {
    manifest {
        attributes(
            'FMLModType': 'LIBRARY'
        )
    }
    from {
        exclude 'META-INF/maven/**'
        exclude '**/versions/9/*'
        exclude 'module-info.class'
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

configurations {
    compile {
        transitive = true
    }
}

shadowJar {
    configurations = [project.configurations.shade]
    dependencies {
        exclude(dependency('org.apache.logging.log4j:log4j-core:.*'))
        exclude(dependency('org.apache.logging.log4j:log4j-core:.*'))
        exclude(dependency('org.apache.logging.log4j:log4j-slf4j18-impl:.*'))
        exclude(dependency('org.apache.commons:commons-lang3:.*'))
        exclude(dependency('com.google.code.gson:.*'))
        exclude(dependency('javax:.*'))
        exclude(dependency('org.jetbrains:.*'))

        if (!project.oshi_hack) {
            exclude(dependency('net.java.dev.jna:.*'))
            exclude 'org/slf4j/**'
        }

        exclude 'META-INF/versions/9/**'
        exclude 'module-info.class'
        exclude 'org/apache/commons/lang3/**'

        relocate 'org.apache.commons.collections4', 'hypshadow.apache.commons.collections4'
        relocate 'javax.annotation', 'hypshadow.javax.annotation'
        relocate 'gnu.trove', 'hypshadow.gnu.trove'
        relocate 'com.fasterxml', 'hypshadow.fasterxml'
        relocate 'club.minnced', 'hypshadow.club.minnced'
        relocate 'com.iwebpp', 'hypshadow.iwebpp'
        relocate 'com.jagrosh', 'hypshadow.jagrosh'
        relocate 'com.neovisionaries', 'hypshadow.neovisionaries'
        relocate 'me.hypherionmc.nightconfig', 'hypshadow.hypherionmc.nightconfig'
        relocate 'me.hypherionmc.jqlite', 'hypshadow.hypherionmc.jqlite'
        relocate 'net.dv8tion', 'hypshadow.dv8tion'
        relocate 'okhttp3', 'hypshadow.okhttp3'
        relocate 'okio', 'hypshadow.okio'
        relocate 'org.json', 'hypshadow.json'
        relocate 'pw.chew', 'hypshadow.chew'
        relocate 'oshi', 'hypshadow.oshi'
        relocate 'kotlin', 'hypshadow.kotlin'
    }
    mergeServiceFiles()
    exclude 'META-INF/maven/**'
    setArchiveClassifier('')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(shadowJar) {
                builtBy shadowJar
            }
        }
    }

    repositories {
        maven {
            url System.getenv("MAVEN_URL")
            credentials {
                username System.getenv("MAVEN_USER")
                password System.getenv("MAVEN_PASS")
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
}

spotless {
    java {
        licenseHeaderFile(rootProject.file("HEADER")).yearSeparator("-")
    }
}
